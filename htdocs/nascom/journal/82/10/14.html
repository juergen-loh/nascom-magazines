<?php columnStart(2, 1); ?>
<pre id="article1" class="donthyphenate">
1 REM  *** LOST DUTCHMAN'S GOLD ***  31.10.82
2 REM
3 REM COPYRIGHT 1980 BY TERI LI.
4 REM AUS BYTE DEZEMBER 1980
5 REM
6 REM BEARBEITET FUER NASCOM VOM MICHAEL BACH
10 REM
11 REM
12 CLEAR 1000: DEF FNG(I)=-I*(I&gt;0)
13 CLS: SCREEN 10,16
14 PRINT"*** LOST DUTCHMAN'S GOLD ***"
19 SCREEN 1,4
20 PRINT"Welcome to the search for the lost";
21 PRINT" Dutchman's"
22 PRINT"gold. I'm the ghost of back-pack Sam,";
23 PRINT" yer guide": PRINT"on this adventure."
24 PRINT"Ya can use two-word commands to find ";
25 PRINT"clues and":PRINT"move about."
26 PRINT"GET, GO, PUT, DROP, SCORE, INVENTORY ";
27 PRINT"`n bout":PRINT"100 more words make ";
32 PRINT"sense to me."
33 PRINT"Ask fer HELP and '?' if ya get stumped."
34 PRINT"I hope ya don't end up a ghost, like me!"
35 PRINT"GOOD LUCK!!":PRINT
36 DIM N$(26,7),O$(32,3),R$(46),SA(32,3)
37 B=0:IN=B:LN=50:LM=LN:X1=B:X2=B:T$="":M1$=T$
38 M2$=T$:QM$=T$:U=B:U1=B:IM=2:BD=21:L=1
39 C$=CHR$(13)
40 FOR J=B TO 26:FOR I=B TO 7:READ N$(J,I)
42 NEXT I:NEXT J
50 FOR I=B TO 32:READ O$(I,0),O$(I,1),O$(I,2)
52 O$(I,3)="":NEXT
60 FOR I=B TO 46:READ R$(I):NEXT
70 INPUT"Resume old game";QM$
72 IF LEFT$(QM$+" ",1)="Y" GOTO 74
73 IF LEFT$(QM$+" ",1)&lt;&gt;"J" GOTO 100
74 PRINT"Start cassette recorder now!"
76 CLOAD*SA
78 PRINT"Stop cassette recorder!"
80 FOR I=0 TO 32
82 FOR J=1 TO 3:Q$=STR$(SA(I,J)):O$(I,J)=Q$
83 IFLEFT$(Q$,1)=" "THENQ$=RIGHT$(Q$,LEN(Q$)-1)
84 O$(I,J)=Q$
85 NEXT J:NEXT I
86 L=SA(0,0):CL=SA(1,0):LN=SA(2,0)
88 IN=SA(3,0):LM=SA(4,0):U=SA(5,0)
90 U1=SA(6,0):IM=SA(7,0)
100 IF VAL(O$(12,2))=L AND CL&gt;0 GOTO 150
101 IF O$(12,3)="1" AND CL&gt;0 GOTO 150
102 IF L&lt;15 GOTO 150
103 PRINT"It's too dark ta see!!"
104 IF L&lt;&gt;15 OR O$(6,3)="1"OR O$(4,3)&lt;&gt;"1"GOTO110
105 PRINT"Ya hear a strange noise!"
110 GOTO 350
120 T$="Ok":RETURN
140 B=1:PRINT
145 PRINT"Ya see: ";: RETURN
150 IF CL=1 THEN LN=LN-1: IF LN&lt;1 THEN CL=0
160 L1=L
170 B=0
</pre>
<?php columnChange(2); ?>
<pre class="donthyphenate">
172 PRINT: PRINT"YER "N$(L,0)"."
173 FOR J=0 TO 32:IF VAL(O$(J,2))&lt;&gt;L GOTO 240
175 IF B=0 THEN GOSUB 140
180 IF O$(J,3)="-7"OR O$(J,3)="-2"THENPRINT"burnt";
190 IF J&lt;&gt;1OR VAL(O$(1,2))&lt;&gt;L THEN 200
193 IF O$(1,3)=""THEN PRINT"untied ";:GOTO200
196 IF O$(1,3)="-9"THEN PRINT"dead ";
200 IF O$(J,3)="-3"THEN PRINT"smashed ";
210 IF L&lt;&gt;26 OR J&lt;&gt;32 THEN 220
213 IF U1&lt;&gt;0 THENPRINT"un";
216 PRINT"locked ";
220 IF CL=1 AND J=12 THEN PRINT"lit ";
230 PRINT O$(J,0)". ";
240 NEXT J
250 IF U1=1 AND L=1 THEN PRINT"Open TRAP DOOR. ";
260 IF N$(L,1)="" THEN PRINT:GOTO330
265 IF B=0 THEN GOSUB 140
270 IF L&lt;&gt;16 THEN 280
273 IF U&lt;&gt;0 THEN PRINT"un";
276 PRINT"locked ";:GOTO 290
280 IF L&lt;&gt;17 THEN 290
283 IF U=1 THEN PRINT"unlocked ";:GOTO 290
286 PRINT"blocked ";
290 PRINTN$(L,1)"."
310 IFL=17ANDU=0THENN$(L,2)=" "+RIGHT$(N$(L,2),3)
320 IFL=16ANDU=1THENN$(L,2)="E"+RIGHT$(N$(L,2),3)
330 PRINT:IF N$(L,2)="" THEN 350
332 PRINT"Obvious exits: ";
333 FOR I=1 TO 4:P$=MID$(N$(L,2),I,1)
334 IF P$="N" THEN PRINT"North ";
335 IF P$="S" THEN PRINT"South ";
337 IF P$="E" THEN PRINT"East ";
338 IF P$="W" THEN PRINT"West ";
340 NEXT: PRINT
350 B=0
351 PRINT"==========================================="
362 IFL&lt;&gt;9 OR O$(22,2)&lt;&gt;"11"GOTO 370
363 PRINT"Thar's noise up ahead,";
364 PRINT"sounds like injuns."
370 IFL=9 AND O$(22,2)="" THEN O$(22,2)="11"
390 INPUT" -- Now what";QM$
410 IF LN&gt;=10 OR CL&lt;&gt;1 GOTO 415
412 PRINT"Yer runnin' low on kerosene."
415 IFQM$="SAVE" THEN 2520
420 IFQM$&lt;&gt;"SCORE" THEN 450
423 IF L=6 THEN 426
424 T$="Ya get nothin' for bein' here":GOTO1900
426 Z=0:FOR I=14 TO 17
427 IF VAL(O$(I,2))=6 THEN Z=Z+1
428 NEXT
440 PRINT"Ya got"Z"treasures, tot'lin"Z/4*100"%"
442 IF Z=4 THEN PRINT"Ya made it!!":GOTO 2220
445 T$="Ya missed sum treasure!":GOTO 1900
450 IF CL&lt;&gt;0 OR L&lt;&gt;15 OR O$(6,3)&lt;&gt;"1" GOTO 460
452 PRINT"Ya gad!":PRINT"Ya been bit by a rattler."
454 PRINT"Yer dead.":GOTO 2220
460 IF L&lt;&gt;17 OR U&lt;&gt;1 OR RND(1)*9+1&gt;=2 GOTO 480
462 PRINT"&amp;(%#&amp;@ CAVE IN !! ?:+*@"
464 PRINT"The iron door's blocked,";
466 PRINT"Yer trapped!!": U=0
</pre>
<?php columnEnd(2); ?>
